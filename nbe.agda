{-# OPTIONS --prop #-}
module nbe where
open import Agda.Builtin.Nat using (Nat; suc; zero)
open import Agda.Builtin.Equality using (_â‰¡_; refl)
open import Data.Product using (_Ã—_; _,_; projâ‚‚)

open import combinator

-- We now take a differerent approach.
-- Instead of blindly following the reduction rules, let's
-- really find out what the combinator ð‘šð‘’ð‘Žð‘›ð‘ .

-- Since these are not part of the final program, I
-- separate them into a private module.
private module Meaning where
    -- What does the types mean?
    -- â„• means the natural numbers Nat, no doubt. And
    -- (Î± â‡’ Î²) should mean the function space.
    -- So we might do the following:
    Meaning : Type -> Set
    Meaning â„• = Nat
    Meaning (Î± â‡’ Î²) = Meaning Î± -> Meaning Î²
    -- Then, we want to ð‘–ð‘›ð‘¡ð‘’ð‘Ÿð‘ð‘Ÿð‘’ð‘¡ the combinators to their Meaning.
    interpret : Term Î± -> Meaning Î±
    interpret O = zero
    interpret S = suc
    interpret â„ = iterate
        where  -- The familiar iterate function.
            iterate : âˆ€ {A : Set} -> Nat -> A -> (A -> A) -> A
            iterate zero a f = a
            iterate (suc n) a f = f (iterate n a f)
    interpret ð•‚ = Î» z _ -> z
    interpret ð•Š = Î» x y z -> x z (y z)
    interpret (M âˆ™ N) = interpret M (interpret N)
    -- All work fine, and the most important thing is that if two things are
    -- considered equal, then their interpretations are equal:
    _ : interpret (ð•‚ âˆ™ ð•‚ âˆ™ ð•€ {â„•}) â‰¡ Î» (x : Nat) (y : Nat) -> x
    _ = refl
    _ : interpret (ð•‚ âˆ™ ð•‚ âˆ™ ð•Š {â„•}{â„•}{â„•}) â‰¡ Î» (x : Nat) (y : Nat) -> x
    _ = refl
    -- (There are some eta-equality related problems that we will not discuss here.)
    -- To make use of the interpretations, we need a way to convert
    -- the interpreted terms into normal forms. In other words:
    --     reify : Meaning Î± -> Term Î±
    -- But we can't do that. This is because in the function spaces,
    -- there are more functions than we can express in the combinator
    -- language! And if we worked in Set theory instead of Agda, there
    -- would be even more of those ghost functions. (Exercise: try to
    -- implement the function reify, and describe the difficulty you
    -- encounter.)

-- What can we do to amend the situation? Actually we just need a
-- very natural change. Note that in our previous implementation
-- in reduce.agda, we only cared about the normal forms, i.e. the syntax.
-- And in the development above, we only cared about the meanings,
-- i.e. the semantics. This suggests the following change...

-- For â„• the meaning stays the same, but for
-- function spaces, we require ð‘ð‘œð‘¡â„Ž a normal form ð‘Žð‘›ð‘‘ a function.
-- In Agda code:
--     Meaning : Type -> Set
--     Meaning â„• = Nat
--     Meaning (Î± â‡’ Î²) = NormalForm (Î± â‡’ Î²) Ã— (Meaning Î± -> Meaning Î²)
-- where NormalForm (which is not yet defined) is the type of normal
-- forms. This is sufficient for programming purposes, and is close to
-- the real-world implementations. But since we are using Agda, we
-- shouldn't confine ourselves to programming. We should simultaneously
-- produce a ð‘ð‘Ÿð‘œð‘œð‘“ that the normal form can indeed be obtained from
-- reducing the original term. This means that we also need to keep track
-- of the original term. (After program extraction, the proofs are
-- erased, so there's no additional cost in the program.)

-- This produces the definition of (Red Î± A), which stands for
-- "A is reducible of type Î±". Our new definition for the Meaning of Î±
-- is exactly the reducible terms.
-- The word "reducible" comes from Tait. We also adopt the convention
-- to use âŸ¦ M âŸ§ to denote the interpretation of M
Red : âˆ€ Î± -> Term Î± -> Set  -- Glue!
Red â„• A = WN A
Red (Î± â‡’ Î²) A = WN A Ã— âˆ€ {B} -> Red Î± B -> Red Î² (A âˆ™ B)

-- We can easily extract the normal form now.
reify : Red Î± A -> WN A
reify {Î± = â„•} AÊ· = AÊ·
reify {Î± = Î± â‡’ Î²} (AÊ· , _) = AÊ·

-- A very interesting lemma: if A reduces to B, and B is reducible,
-- then A is also reducible.
RedCl : (A âŸ¶ B) -> Red Î± B -> Red Î± A
RedCl {Î± = â„•} R (wn Î½ R') = wn Î½ (R â€ R')
RedCl {Î± = Î± â‡’ Î²} R (wn Î½ R' , F) = wn Î½ (R â€ R') ,
    Î» âŸ¦CâŸ§ -> RedCl (map appâ‚— R) (F âŸ¦CâŸ§)

âŸ¦ð•‚âŸ§ : Red Î± A -> Red Î² B -> Red Î± (ð•‚ âˆ™ A âˆ™ B)
âŸ¦ð•‚âŸ§ âŸ¦AâŸ§ âŸ¦BâŸ§ = RedCl (single (red ð•‚)) âŸ¦AâŸ§

âŸ¦ð•‚â‚âŸ§ : Red Î± A -> Red (Î² â‡’ Î±) (ð•‚ âˆ™ A)
âŸ¦ð•‚â‚âŸ§ âŸ¦AâŸ§ with reify âŸ¦AâŸ§
... | wn Î½ R = wn (ð•‚â‚ Î½) (map appáµ£ R) , âŸ¦ð•‚âŸ§ âŸ¦AâŸ§

âŸ¦ð•‚â‚€âŸ§ : Red (Î± â‡’ Î² â‡’ Î±) ð•‚
âŸ¦ð•‚â‚€âŸ§ = wn ð•‚â‚€ refl , âŸ¦ð•‚â‚âŸ§

âŸ¦ð•ŠâŸ§ : Red (Î± â‡’ Î² â‡’ Î³) A
    -> Red (Î± â‡’ Î²) B
    -> Red Î± C
    -> Red Î³ (ð•Š âˆ™ A âˆ™ B âˆ™ C)
âŸ¦ð•ŠâŸ§ âŸ¦AâŸ§ âŸ¦BâŸ§ âŸ¦CâŸ§ = RedCl (single (red ð•Š)) $
    (âŸ¦AâŸ§ .projâ‚‚ âŸ¦CâŸ§) .projâ‚‚ (âŸ¦BâŸ§ .projâ‚‚ âŸ¦CâŸ§)
-- See how everything passes though without the need for the TERMINATING pragma?

âŸ¦ð•Šâ‚‚âŸ§ : Red (Î± â‡’ Î² â‡’ Î³) A -> Red (Î± â‡’ Î²) B -> Red (Î± â‡’ Î³) (ð•Š âˆ™ A âˆ™ B)
âŸ¦ð•Šâ‚‚âŸ§ âŸ¦AâŸ§@(wn Î½â‚ Râ‚ , Fâ‚) âŸ¦BâŸ§@(wn Î½â‚‚ Râ‚‚ , Fâ‚‚)
    = wn (ð•Šâ‚‚ Î½â‚ Î½â‚‚) (map appáµ£ Râ‚‚ â€ map (appâ‚— âˆ˜ appáµ£) Râ‚) , âŸ¦ð•ŠâŸ§ âŸ¦AâŸ§ âŸ¦BâŸ§

âŸ¦ð•Šâ‚âŸ§ : Red (Î± â‡’ Î² â‡’ Î³) A -> Red ((Î± â‡’ Î²) â‡’ (Î± â‡’ Î³)) (ð•Š âˆ™ A)
âŸ¦ð•Šâ‚âŸ§ âŸ¦AâŸ§@(wn Î½ R , F) = wn (ð•Šâ‚ Î½) (map appáµ£ R) , âŸ¦ð•Šâ‚‚âŸ§ âŸ¦AâŸ§

âŸ¦ð•Šâ‚€âŸ§ : Red ((Î± â‡’ Î² â‡’ Î³) â‡’ (Î± â‡’ Î²) â‡’ (Î± â‡’ Î³)) ð•Š
âŸ¦ð•Šâ‚€âŸ§ = wn ð•Šâ‚€ refl , âŸ¦ð•Šâ‚âŸ§

âŸ¦â„_âŸ§ : âˆ€ n -> Red Î± B -> Red (Î± â‡’ Î±) C -> Red Î± (â„ âˆ™ (# n) âˆ™ B âˆ™ C)
âŸ¦â„ zero âŸ§ âŸ¦BâŸ§ âŸ¦CâŸ§ = RedCl (single (red â„0)) âŸ¦BâŸ§
âŸ¦â„ suc n âŸ§ âŸ¦BâŸ§ âŸ¦CâŸ§ = RedCl (single (red â„S)) $
    âŸ¦CâŸ§ .projâ‚‚ (âŸ¦â„ n âŸ§ âŸ¦BâŸ§ âŸ¦CâŸ§)

âŸ¦â„âŸ§ : Red â„• A -> Red Î± B -> Red (Î± â‡’ Î±) C -> Red Î± (â„ âˆ™ A âˆ™ B âˆ™ C)
âŸ¦â„âŸ§ (wn (â„• n) R) âŸ¦BâŸ§ âŸ¦CâŸ§ =
    RedCl (map (appâ‚— âˆ˜ appâ‚— âˆ˜ appáµ£) R) (âŸ¦â„ n âŸ§ âŸ¦BâŸ§ âŸ¦CâŸ§)

âŸ¦â„â‚‚âŸ§ : Red â„• A -> Red Î± B -> Red ((Î± â‡’ Î±) â‡’ Î±) (â„ âˆ™ A âˆ™ B)
âŸ¦â„â‚‚âŸ§ âŸ¦AâŸ§@(wn Î½â‚ Râ‚) âŸ¦BâŸ§ with reify âŸ¦BâŸ§
... | wn Î½â‚‚ Râ‚‚ = wn (â„â‚‚ Î½â‚ Î½â‚‚) (map appáµ£ Râ‚‚ â€ map (appâ‚— âˆ˜ appáµ£) Râ‚) , âŸ¦â„âŸ§ âŸ¦AâŸ§ âŸ¦BâŸ§

âŸ¦â„â‚âŸ§ : Red â„• A -> Red (Î± â‡’ (Î± â‡’ Î±) â‡’ Î±) (â„ âˆ™ A)
âŸ¦â„â‚âŸ§ âŸ¦AâŸ§@(wn Î½ R) = wn (â„â‚ Î½) (map appáµ£ R) , âŸ¦â„â‚‚âŸ§ âŸ¦AâŸ§

âŸ¦â„â‚€âŸ§ : Red (â„• â‡’ Î± â‡’ (Î± â‡’ Î±) â‡’ Î±) â„
âŸ¦â„â‚€âŸ§ = wn â„â‚€ refl , âŸ¦â„â‚âŸ§

âŸ¦SâŸ§ : Red â„• A -> Red â„• (S âˆ™ A)
âŸ¦SâŸ§ (wn (â„• n) R) = wn (â„• (suc n)) (map appáµ£ R)

-- Finally, we collect everything together.
-- Read as a theorem: Every term is reducible;
-- Read as a program: A program that calculates the meaning of the terms.
âŸ¦_âŸ§ : âˆ€ A -> Red Î± A
âŸ¦ A âˆ™ B âŸ§ = âŸ¦ A âŸ§ .projâ‚‚ âŸ¦ B âŸ§
âŸ¦ ð•‚ âŸ§ = âŸ¦ð•‚â‚€âŸ§
âŸ¦ ð•Š âŸ§ = âŸ¦ð•Šâ‚€âŸ§
âŸ¦ â„ âŸ§ = âŸ¦â„â‚€âŸ§
âŸ¦ O âŸ§ = wn (â„• zero) refl
âŸ¦ S âŸ§ = wn Sâ‚€ refl , âŸ¦SâŸ§

-- We can also get a normalizing function that throws away the proof.
normalize : Term Î± -> Term Î±
normalize A with reify âŸ¦ A âŸ§
... | wn {B = B} _ _ = B

_ : normalize (Mult âˆ™ # 100 âˆ™ # 100) â‰¡ # 10000
_ = refl

-- Recall that we defined Red in terms of WN. Actually, replacing WN with
-- SN, the proof also works, except for some tweaks. This then proves the
-- strong normalization theorem. It is left as an exercise for the reader.
